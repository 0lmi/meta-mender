#@TYPE: Machine
#@NAME: vexpress-qemu

#@DESCRIPTION: Machine configuration for QEMU running vexpress with system on MTD device

require conf/machine/include/qemu.inc
require conf/machine/include/vexpress.inc

IMAGE_FSTYPES_append = " ubifs ubi vexpress-nor"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "kernel-image kernel-devicetree"
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "mtd-utils mtd-utils-ubifs mtd-utils-jffs2"

MKUBIFS_ARGS = "-m 1 -e 262016 -c 1024"
UBINIZE_ARGS = "-p 256KiB -m 1 -O 64"

# NOTE: normally we'd pick up overrides for vexpress-qemu, disable them for now
# to avoid incorrect u-boot patches being applied (on a side note, maybe rework
# u-boot patches to be applied for any machine?)

#MACHINEOVERRIDES =. "vexpress-qemu-flash:vexpress-qemu:"

# Needed to skip particular QA checks that don't apply to U-boot's binary.
INSANE_SKIP_u-boot += "ldflags textrel"

# 16MB for data partition
MENDER_DATA_PART_SIZE_MB = "16"
# combined NOR flash is 128MB
MENDER_STORAGE_TOTAL_SIZE_MB = "128"
# align to PEB size 256k
MENDER_PARTITION_ALIGNMENT_KB = "256"

# MTD partitioning has the following layout:
# 1m(u-boot)ro,
# 1m(u-boot-env)ro,
# -(ubi)
#
# NOTE: this affects only u-boot's autoconfigured headers, the same variable is
# also used for generating fw_env.config but because of assumptions made in
# meta-mender-core/recipes-bsp/u-boot/u-boot-fw-utils-mender.inc we need ship
# our own config instead.
MENDER_UBOOT_ENV_STORAGE_DEVICE_OFFSET = "0x100000"

# use ubifs volumes for generating mender artifacts
ARTIFACTIMG_FSTYPE = "ubifs"

# add support for generating NOR Image files
IMAGE_CLASSES += "vexpress-nor_image"
